buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.11'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.0.0"
    }
}
description = 'A Markup (Markdown, AsciiDoc) document builder'
version = '0.1.6-SNAPSHOT'
group = 'io.github.robwin'

apply plugin: 'java'
apply plugin: 'org.asciidoctor.convert'
apply from: 'gradle/publishing.gradle'
apply from: 'gradle/coverage.gradle'

tasks.withType(JavaCompile) {
    sourceCompatibility = "1.7"
    targetCompatibility = "1.7"
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked"
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.18'
    compile 'org.apache.commons:commons-collections4:4.1'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile "commons-io:commons-io:2.4"
    compile "commons-codec:commons-codec:1.10"
    testCompile 'junit:junit:4.11'
    testCompile 'io.github.robwin:assertj-diff:0.1.1'
    testCompile "org.mockito:mockito-core:1.9.5"
    testCompile 'ch.qos.logback:logback-classic:1.1.6'
    testCompile 'io.github.robwin:assertj-diff:0.1.1'
}

asciidoctor {
    backends = ['html5', 'pdf']
    attributes = [
            doctype: 'book',
            toc: 'left',
            toclevels: '2',
            numbered: ''
    ]
    sourceDir "build/tmp"
}

tasks.asciidoctor {
    dependsOn 'check'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}
